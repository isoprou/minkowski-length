 //Lemma 3.2 Part 1: Here we rule out the case a+b>28. Under this
 //assumption we showed that we can assume that  the direction vector 
 //(x,y,z) of a primitive segment I satisfies z=0, x>= 0, y>= 0, x,y<=14. Due 
 //to symmetry, we can also assume y<=x. Let P=conv{e1,e2,e3}. We show 
 //here that  L(P+I)=2 only happens when (x,y,z)=(1,0,0), (0,1,0), or (1,1,0).
 
 //The output is (1,0,0) and (1,1,0). Note that we assumed that x<=y.
 //Without this assumption, we get one more answer -- (0,1,0).

load "AddTriangleHuh.mgm";
load "AddTetraHuh.mgm";

t:= Cputime();
P:=Polytope([[1,0,0],[0,1,0],[0,0,1]]);
for x in [0..14] do
    for y in [0..x] do
    	I:=Polytope([[0,0,0],[x,y,0]]);
        if x+y gt 0 then
            if GCD(x,y) eq 1 then
                if minktwo(P+I) then
                    print([x,y,0]);
                end if;
            end if;
        end if;
    end for;
end for;
 Cputime(t);
 
//OUTPUT:
//[ 1, 0, 0 ]
//[ 1, 1, 0 ]
//5.830



//Part 2: Here we check for P=[(1,0,0),(0,1,0),(0,0,1),(a,b,1)], where 0\leq a\leq b and
//a+b\leq 28, whether there exists Q with |Q|=3 or |Q|=4 such that
//L({P+Q)=2. We exclude first cases a=b=1 and a=0, b=1 since they take a bit
//of time (and are not needed for the conclusion of the Lemma). We run them
//separately below.
//


//If for P=[(1,0,0),(0,1,0),(0,0,1),(a,b,1)] we find a triangle Q we print [a,b,3].
//If for such P we find a 4-point Q we print [a,b,4]. Note that Vol(P)=a+b.

t:= Cputime();
for b in [2..28] do
    for a in [0..Min(b,28-b)] do
        if GCD(a,b) eq 1 then
	   P:=Polytope([[1,0,0],[0,1,0],[0,0,1],[a,b,1]]);
            if AddTriangleHuh(P) then
                print([a,b,3]);
                if AddTetraHuh(P) then
                    print([a,b,4]);
                end if;
            end if;
        end if;
    end for;
end for;
Cputime(t);

//OUTPUT:
//[ 1, 2, 3 ]
//[ 1, 3, 3 ]
//[ 2, 3, 3 ]
//1235.560

//The output for the above is [1,2,3], [1,3,3], and [2,3,3], which means that in the case
//when Q is a triangle we have Vol(P)=a+b<=5 and in the case when Q is a
//tetrahedron Vol(P)=a+b<=2.
//


// Case a=b=1

t:= Cputime();
P:=Polytope([[1,0,0],[0,1,0],[0,0,1],[1,1,1]]);
if AddTriangleHuh(P) then
   print([1,1,3]);
   if AddTetraHuh(P) then
      print([1,1,4]);
    end if;
end if;
 Cputime(t);
 
//OUTPUT:
//[ 1, 1, 3 ]
//[ 1, 1, 4 ]
//620.380
           
// Case a=0, b=1

t:= Cputime();
P:=Polytope([[1,0,0],[0,1,0],[0,0,1],[0,1,1]]);
if AddTriangleHuh(P) then
   print([0,1,3]);
   if AddTetraHuh(P) then
      print([0,1,4]);
    end if;
end if;
 Cputime(t);
 
//OUTPUT:
//[ 0, 1, 3 ]
//[ 0, 1, 4 ]
//2339.620
